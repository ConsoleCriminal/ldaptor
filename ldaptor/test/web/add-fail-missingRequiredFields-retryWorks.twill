# textareas reshown on forms with error get spurious \n prefixes and
# suffixes with tidy
config use_tidy 0

formvalue go baseDN ou=People,dc=example,dc=com
submit
code 200

follow "add new entry"
code 200
title "Login"

formvalue login name jack
formvalue login password secret
submit
code 200
title "Ldaptor Add Page"

formvalue add structuralObjectClass account
formvalue add auxiliary_posixAccount True
submit
code 200
title "Ldaptor Add Page"


# required fields for posixAccount are enforced
formvalue add add_uid justfortest
submit
code 200
find '<div class="freeform-form-error">Error:</div>'
find '<span class="freeform-input"><textarea id="add-add_cn" rows="8" class="freeform-textarea" name="add_cn" cols="40"></textarea></span><div class="freeform-error">Please enter a string\.</div>'
find '<span class="freeform-input"><input id="add-add_homeDirectory" type="text" class="freeform-input-text" value="" name="add_homeDirectory" /></span><div class="freeform-error">Please enter a string\.</div>'

find '<label class="freeform-label" for="add-add_uid">uid\*</label><span class="freeform-input"><textarea id="add-add_uid" rows="8" class="freeform-textarea" name="add_uid" cols="40">justfortest</textarea></span>'
find 'No error for error key: add\.add_uid'

# we purposefully do not re-set add_uid here, as
# letting it preserve the old form value triggers
# a bug where the value was prefixed and suffixed
# with a newline. still unclear whether the bug is
# in twill or in ldaptor
formvalue add add_cn 'Just For Test'
formvalue add add_homeDirectory /home/testusers/justfortest
submit
code 200
notfind '<div class="freeform-error">Please enter a string\.</div>'
find '<span>Added <a href="[^"]*">uid=justfortest,ou=People,dc=example,dc=com</a> \[[^]]+\]</span>'

follow Search
code 200
title "Ldaptor Search Page"

formvalue search search_Name justfortest
submit
code 200

find "<p><span>1</span> entries matched\.</p>"
follow /ou%3DPeople%2Cdc%3Dexample%2Cdc%3Dcom/delete/uid%3Djustfortest%2Cou%3DPeople%2Cdc%3Dexample%2Cdc%3Dcom$

code 200
title "Ldaptor Delete Page"
find "<p>Remove <span>uid=justfortest,ou=People,dc=example,dc=com</span>\?</p>"

formvalue delete delete dummy
submit
code 200
title "Ldaptor Search Page"

find '<p>Deleted uid=justfortest,ou=People,dc=example,dc=com\.</p>'

formvalue search search_Name justfortest
submit
code 200

find "<p><span>0</span> entries matched\.</p>"
